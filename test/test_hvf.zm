module test_hvf;

State test_handler
{
    Timer timeout
    {
        on_expire()
        {
            stopLookingTimeClock(expire_time);
            experiment.test.timedOut(expire_time);
        }
    }

    on_entry()
    {
        faces = face_images;
        movie = movie2_frames;
        prepareTestGraphics();
    }

    on_exit()
    {
        timeout.abort();
        eraseLookingTimeClock();
        arrowSymbolOff(int(FRONT));
        unloadImages();
    }

    on_event()
    {
        if (event_code == EVENT_START)
        {
            switch_to_state(attention);
        }
    }

    State attention
    {
        on_entry()
        {
            startAttentionGetter(now());
            arrowSymbolOn(int(FRONT));
        }

        on_event()
        {
            if (event_code == EVENT_START)
            {
                stopAttentionGetter();
                startLookingTimeClock(event_time + 5ms);
                experiment.test.beginTrial(event_time + 5ms);
                switch_to_state(looking);
            }
        }
    }

    State looking
    {
        on_event()
        {
            if (event_code == EVENT_STOP)
            {
                timeout.start(event_time + MAX_LOOK_AWAY_DURATION);
                stopLookingTimeClock(event_time);
                switch_to_state(looking_away);
            }
        }
    }

    State looking_away
    {
        on_event()
        {
            if (event_code == EVENT_START)
            {
                timeout.abort();
                continueLookingTimeClock(event_time);
                switch_to_state(looking);
            }
        }
    }
}
